plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
    id "org.jetbrains.grammarkit" version "2019.3"
}
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.12.9'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.scala-lang:scala-library:2.12.9"

    // https://mvnrepository.com/artifact/org.scala-lang/scala-compiler
    compile group: 'org.scala-lang', name: 'scala-compiler', version: '2.12.9'

    // https://mvnrepository.com/artifact/org.scala-lang/scala-library
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.12.9'

    // https://mvnrepository.com/artifact/org.scala-lang.modules/scala-parser-combinators
    compile group: 'org.scala-lang.modules', name: 'scala-parser-combinators_2.12', version: '1.1.2'

    // https://mvnrepository.com/artifact/org.scala-lang/scala-reflect
    compile group: 'org.scala-lang', name: 'scala-reflect', version: '2.12.9'

    // https://mvnrepository.com/artifact/org.scala-lang.modules/scala-xml
    compile group: 'org.scala-lang.modules', name: 'scala-xml_2.12', version: '1.0.6'

    // compile files(fileTree(dir: 'lib', includes: ['*.jar']))
    // compile files("mmt.jar") // For debugging purposes only!
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    version '2019.2.+'
    pluginName 'MMT'
    plugins 'org.intellij.scala:2019.2.13','java'
}
group 'info.kwarc.mmt.intellij'
version '19.0.0'
patchPluginXml {
    changeNotes """
        <ul>
            <li>
                Additions:
                <ul>
                    <li>Compatbility with MMT 19.0.0</li>
                    <li>Compatbility with IntelliJ IDEA 2019.3.*</li>
                    <li>Extended grammar to cover more of current MMT surface syntax for syntax highlighting</li>
                    <li>Ability to generate MMT surface syntax for items in the sidekick (<a href="https://uniformal.github.io//doc/applications/intellij/index.html#usage-of-the-sidekick">docs</a>)</li>
                </ul>
            </li>
            <li>
                Changes:
                <ul>
                    <li>Dropped support for MMT 18.0.0</li>
                    <li>Internally refactored calls to deprecated IntelliJ Plugin SDK APIs</li>
                </ul>
            </li>
        </ul>
      """

    // See [1-2] for what the numbers encode and esp. [2] to get the actual numbers
    //
    // [1]: https://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html
    // [2]: https://www.jetbrains.com/idea/download/other.html
    sinceBuild "192.6262.58"
    untilBuild "201.*"
}

apply plugin: 'scala'

sourceSets {
    main {
        java {
            // Explicitly set to empty list because sources in src/main/java
            // refer to Scala sources in src/main/scala.
            // Hence let the Scala compiler compile both source directories.
            srcDirs = []
        }
        scala {
            // Even though we later 'src/main/gen' as a "generated sources dir"
            // in this Gradle file, we need to first add it here to make the former
            // work!
            // Related: https://discuss.gradle.org/t/how-do-i-get-intellij-to-recognize-gradle-generated-sources-dir/16847/5
            srcDirs = ['src/main/scala', 'src/main/java', 'src/main/gen']
        }
    }
}

apply plugin: 'idea'
idea {
    module {
        generatedSourceDirs += file('src/main/gen')
    }
}

def publishCredentialsPropertiesFile = file("gradle.properties")
if(publishCredentialsPropertiesFile.exists()) {
    apply from: publishCredentialsPropertiesFile.path
    publishPlugin {
        // channels 'devel'
        token intellijPublishToken
    }
} else {
    logger.warn(publishCredentialsPropertiesFile.path + " does not exist, publishPlugin task will not be created! Copy gradle.properties.template in the same folder to gradle.properties and configure its contents. See file contents for more info.")
}

/*
Non-working automation of grammar creation
===========================================

apply plugin: 'org.jetbrains.grammarkit'
// import is optional to make task creation easier
// Even though IntelliJ marks this import as useless, it is not!
import org.jetbrains.grammarkit.tasks.*

grammarKit {
    // version of IntelliJ patched JFlex (see bintray link below), Default is 1.7.0-1
    jflexRelease = '1.7.0-1'

    // tag or short commit hash of Grammar-Kit to use (see link below). Default is 2017.1.7
    grammarKitRelease = '2017.1.7'
}

task generateMMTLexer(type: GenerateLexer) {
    // source flex file
    source = "src/main/scala/info/kwarc/mmt/intellij/language/psi/mmt.flex"

    // target directory for lexer
    targetDir = "src/main/gen/info/kwarc/mmt/intellij/language/"

    // target classname, target file will be targetDir/targetClass.java
    targetClass = "MMTLexer"

    purgeOldFiles = true
}

task generateMMTParser(type: GenerateParser) {
    // source bnf file
    source = "src/main/scala/info/kwarc/mmt/intellij/language/psi/mmt.bnf"

    // optional, task-specific root for the generated files. Default: none
    targetRoot = 'src/main/gen'

    // path to a parser file, relative to the targetRoot  
    pathToParser = '/info/kwarc/mmt/intellij/language/MMTParser.java'

    // path to a directory with generated psi files, relative to the targetRoot 
    pathToPsiRoot = '/info/kwarc/mmt/intellij/language/psi'

    // if set, plugin will remove a parser output file and psi output directory before generating new ones. Default: false
    purgeOldFiles = true
}*/
